# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    mam.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Tcltest test suite for simlib(n) mam(n)
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n)

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.2 
    eval ::tcltest::configure $argv
}

# Import tcltest(n)
namespace import ::tcltest::test
 
#-----------------------------------------------------------------------
# Load the package to be tested

source ../../lib/simlib/pkgModules.tcl
namespace import ::marsutil::*
namespace import ::simlib::*

#-------------------------------------------------------------------
# Set up the test environment

mam mymam

# cleanup

proc cleanup {} {
    mymam clear
}

#-------------------------------------------------------------------
# playbox get

test playbox_get-1.1 {retrieve all values} -body {
    mymam playbox get
} -cleanup {
    cleanup
} -result {gamma 1.0}

test playbox_get-1.2 {retrieve one attribute} -body {
    mymam playbox get gamma
} -cleanup {
    cleanup
} -result {1.0}

test playbox_get-1.3 {bad attribute gamma} -body {
    catch {mymam playbox get nonesuch}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# playbox set

test playbox_set-1.1 {set one value} -body {
    mymam playbox set gamma 2.0
    mymam playbox get gamma
} -cleanup {
    cleanup
} -result {2.0}

test playbox_set-1.2 {set with dict} -body {
    mymam playbox set [dict create gamma 2.0]
    mymam playbox get
} -cleanup {
    cleanup
} -result {gamma 2.0}

test playbox_set-1.3 {bad attribute name} -body {
    mymam playbox set nonesuch 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid playbox attribute: nonesuch}

test playbox_set-1.4 {marks changed} -setup {
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam playbox set gamma 2.0
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# playbox cget

test playbox_cget-1.1 {retrieves attribute} -body {
    set a [mymam playbox get gamma]
    set b [mymam playbox cget -gamma]

    expr {$a eq $b}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# playbox configure

test playbox_configure-1.1 {sets attributes} -body {
    mymam playbox configure \
        -gamma  2.0

    mymam playbox get
} -cleanup {
    cleanup
} -result {gamma 2.0}

#-------------------------------------------------------------------
# playbox view

test playbox_view-1.1 {returns formatted playbox dict} -body {
    mymam playbox view
} -cleanup {
    cleanup
} -result {gamma 1.00}

        

#-------------------------------------------------------------------
# system ids

# system ids is tested by the tests for [mymam system add] and 
# [mymam system delete]

#-------------------------------------------------------------------
# system add

test system_add-1.1 {initially no systems} -body {
    mymam system ids
} -cleanup {
    cleanup
} -result {}

test system_add-1.2 {added system is in mymam system ids} -body {
    mymam system add 1
    mymam system add 2
    mymam system ids
} -cleanup {
    cleanup
} -result {1 2}

test system_add-1.3 {default settings} -body {
    mymam system add 1
    mymam system get 1
} -cleanup {
    cleanup
} -result {name {System 1} commonality 1.0}

test system_add-1.4 {marks changed} -setup {
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam system add 1
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}

#-------------------------------------------------------------------
# system namedict

test system_namedict-1.1 {initially no systems} -body {
    mymam system namedict
} -cleanup {
    cleanup
} -result {}

test system_namedict-1.2 {existing systems} -body {
    mymam system add 1
    mymam system add 2
    mymam system namedict
} -cleanup {
    cleanup
} -result {1 {System 1} 2 {System 2}}

#-------------------------------------------------------------------
# system get

test system_get-1.1 {retrieve all values} -setup {
    mymam system add 1
} -body {
    mymam system get 1
} -cleanup {
    cleanup
} -result {name {System 1} commonality 1.0}

test system_get-1.2 {retrieve one attribute} -setup {
    mymam system add 1
} -body {
    mymam system get 1 name
} -cleanup {
    cleanup
} -result {System 1}

test system_get-1.3 {bad attribute name} -setup {
    mymam system add 1
} -body {
    catch {mymam system get 1 nonesuch}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# system set

test system_set-1.1 {set one value} -setup {
    mymam system add 1
} -body {
    mymam system set 1 name "Fred"
    mymam system get 1 name
} -cleanup {
    cleanup
} -result {Fred}

test system_set-1.2 {set multiple attributes} -setup {
    mymam system add 1
} -body {
    mymam system set 1 name "Fred" commonality 0
    mymam system get 1
} -cleanup {
    cleanup
} -result {name Fred commonality 0}

test system_set-1.3 {set with dict} -setup {
    mymam system add 1
} -body {
    mymam system set 1 [dict create name "Fred" commonality 0]
    mymam system get 1
} -cleanup {
    cleanup
} -result {name Fred commonality 0}

test system_set-1.4 {bad system} -setup {
    mymam system add 1
} -body {
    mymam system set nonesuch commonality 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid system}

test system_set-1.4 {bad attribute name} -setup {
    mymam system add 1
} -body {
    mymam system set 1 nonesuch 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid system attribute: nonesuch}

test system_set-1.5 {marks changed} -setup {
    mymam system add 1
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam system set 1 name "Fred"
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}

#-------------------------------------------------------------------
# system delete

test system_delete-1.1 {sid removed from list of ids} -setup {
    mymam system add 1
    mymam system add 2
} -body {
    mymam system delete 2
    mymam system ids
} -cleanup {
    cleanup
} -result {1}

test system_delete-1.2 {sid no longer exists} -setup {
    mymam system add 1
    mymam system add 2
} -body {
    set a [mymam system exists 2]
    mymam system delete 2
    set b [mymam system exists 2]

    list $a $b
} -cleanup {
    cleanup
} -result {1 0}

test system_delete-1.3 {marks changed} -setup {
    mymam system add 1
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam system delete 1
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# system undelete

test system_undelete-1.1 {can undelete most recent system} -setup {
    mymam topic add 1
    mymam topic add 2
    mymam system add 1
    mymam system add 2
    mymam topic configure 2 -relevance 0.5
    mymam belief configure 1 2 -position 0.3
    mymam belief configure 2 2 -position 0.6
} -body {
    set oldDump [mymam dump]
    set undoData [mymam system delete 2]
    set sid [mymam system undelete $undoData]
    set newDump [mymam dump]

    # TBD: Not working!  why not?
    expr {$newDump eq $oldDump}
} -cleanup {
    cleanup
} -result {1}

test system_undelete-1.2 {can undelete older system} -setup {
    mymam topic add 1
    mymam topic add 2
    mymam system add 1
    mymam system add 2
    mymam topic configure 2 -relevance 0.5
    mymam belief configure 1 2 -position 0.3
    mymam belief configure 2 2 -position 0.6
} -body {
    set oldDump [mymam dump]
    set undoData [mymam system delete 1]
    set sid [mymam system undelete $undoData]
    set newDump [mymam dump]
    expr {1 == 1 && $newDump eq $oldDump}
} -cleanup {
    cleanup
} -result {1}

test system_undelete-1.3 {marks changed} -setup {
    mymam system add 1
    set undoData [mymam system delete 1]
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam system undelete $undoData
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}

#-------------------------------------------------------------------
# system cget

test system_cget-1.1 {retrieves attribute} -setup {
    mymam system add 1
} -body {
    set a [mymam system get 1 name]
    set b [mymam system cget 1 -name]

    expr {$a eq $b}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# system configure

test system_configure-1.1 {sets attributes} -setup {
    mymam system add 1
} -body {
    mymam system configure 1 \
        -name         Fred     \
        -commonality  0

    mymam system get 1
} -cleanup {
    cleanup
} -result {name Fred commonality 0}

#-------------------------------------------------------------------
# system id

test system_id-1.1 {retrieves ID} -setup {
    mymam system add 1
    mymam system configure 1 -name "John Doe" 
} -body {
    mymam system id "John Doe"
} -cleanup {
    cleanup
} -result {1}

test system_id-1.2 {no such system} -setup {
    mymam system add 1
    mymam system configure 1 -name "John Doe" 
} -body {
    mymam system id "Jane Doe"
} -cleanup {
    cleanup
} -result {}

#-------------------------------------------------------------------
# system view

test system_view-1.1 {retrieves the view dict} -setup {
    mymam system add 1
} -body {
    mymam system view 1
} -cleanup {
    cleanup
} -result {name {System 1} commonality 1.00 sid 1}


#-------------------------------------------------------------------
# topic ids

# topic ids is tested by the tests for [mymam topic add] and 
# [mymam topic delete]

#-------------------------------------------------------------------
# topic add


test topic_add-1.1 {initially no topics} -body {
    mymam topic ids
} -cleanup {
    cleanup
} -result {}

test topic_add-1.2 {added topic is in mymam topic ids} -body {
    mymam topic add 1
    mymam topic add 2
    mymam topic ids
} -cleanup {
    cleanup
} -result {1 2}

test topic_add-1.3 {default settings} -body {
    mymam topic add 1
    mymam topic get 1
} -cleanup {
    cleanup
} -result {name {Topic 1} affinity 1 relevance 1.0}

test topic_add-1.4 {marks changed} -setup {
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam topic add 1
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}

#-------------------------------------------------------------------
# topic get

test topic_get-1.1 {retrieve all values} -setup {
    mymam topic add 1
} -body {
    mymam topic get 1
} -cleanup {
    cleanup
} -result {name {Topic 1} affinity 1 relevance 1.0}

test topic_get-1.2 {retrieve one attribute} -setup {
    mymam topic add 1
} -body {
    mymam topic get 1 name
} -cleanup {
    cleanup
} -result {Topic 1}

test topic_get-1.3 {bad attribute name} -setup {
    mymam topic add 1
} -body {
    catch {mymam topic get 1 nonesuch}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# topic set

test topic_set-1.1 {set one value} -setup {
    mymam topic add 1
} -body {
    mymam topic set 1 name "Fred"
    mymam topic get 1 name
} -cleanup {
    cleanup
} -result {Fred}

test topic_set-1.2 {set multiple attributes} -setup {
    mymam topic add 1
} -body {
    mymam topic set 1 name "Fred" affinity 0
    mymam topic get 1
} -cleanup {
    cleanup
} -result {name Fred affinity 0 relevance 1.0}

test topic_set-1.3 {set with dict} -setup {
    mymam topic add 1
} -body {
    mymam topic set 1 [dict create name "Fred" affinity 0]
    mymam topic get 1
} -cleanup {
    cleanup
} -result {name Fred affinity 0 relevance 1.0}

test topic_set-1.4 {bad topic} -setup {
    mymam topic add 1
} -body {
    mymam topic set nonesuch affinity 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid topic}

test topic_set-1.4 {bad attribute name} -setup {
    mymam topic add 1
} -body {
    mymam topic set 1 nonesuch 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid topic attribute: nonesuch}

test topic_set-1.5 {marks changed} -setup {
    mymam topic add 1
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam topic set 1 relevance 0.5
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# topic delete

test topic_delete-1.1 {tid removed from list of ids} -setup {
    mymam topic add 1
    mymam topic add 2
} -body {
    mymam topic delete 2
    mymam topic ids
} -cleanup {
    cleanup
} -result {1}

test topic_delete-1.2 {tid no longer exists} -setup {
    mymam topic add 1
    mymam topic add 2
} -body {
    set a [mymam topic exists 2]
    mymam topic delete 2
    set b [mymam topic exists 2]

    list $a $b
} -cleanup {
    cleanup
} -result {1 0}

test topic_delete-1.3 {marks changed} -setup {
    mymam topic add 1
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam topic delete 1
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# topic undelete

test topic_undelete-1.1 {can undelete most recent topic} -setup {
    mymam system add 1
    mymam system add 2
    mymam topic add 1
    mymam topic add 2
    mymam topic configure 2 -relevance 0.5
    mymam belief configure 1 2 -position 0.3
    mymam belief configure 2 2 -position 0.6
} -body {
    set oldDump [mymam dump]
    set undoData [mymam topic delete 2]
    set tid [mymam topic undelete $undoData]
    set newDump [mymam dump]
    expr {$newDump eq $oldDump}
} -cleanup {
    cleanup
} -result {1}

test topic_undelete-1.2 {can undelete most older topic} -setup {
    mymam system add 1
    mymam system add 2
    mymam topic add 1
    mymam topic add 2
    mymam topic configure 2 -relevance 0.5
    mymam belief configure 1 2 -position 0.3
    mymam belief configure 2 2 -position 0.6
} -body {
    set oldDump [mymam dump]
    set undoData [mymam topic delete 1]
    set tid [mymam topic undelete $undoData]
    set newDump [mymam dump]
    expr {$newDump eq $oldDump}
} -cleanup {
    cleanup
} -result {1}

test topic_delete-1.3 {marks changed} -setup {
    mymam topic add 1
    set undoData [mymam topic delete 1]
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam topic undelete $undoData
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# topic cget

test topic_cget-1.1 {retrieves attribute} -setup {
    mymam topic add 1
} -body {
    set a [mymam topic get 1 name]
    set b [mymam topic cget 1 -name]

    expr {$a eq $b}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# topic configure

test topic_configure-1.1 {sets attributes} -setup {
    mymam topic add 1
} -body {
    mymam topic configure 1 \
        -name      Fred       \
        -affinity  0          \
        -relevance 0.5

    mymam topic get 1
} -cleanup {
    cleanup
} -result {name Fred affinity 0 relevance 0.5}

#-------------------------------------------------------------------
# topic id

test topic_id-1.1 {retrieves ID} -setup {
    mymam topic add 1
    mymam topic configure 1 -name "John Doe" 
} -body {
    mymam topic id "John Doe"
} -cleanup {
    cleanup
} -result {1}

test topic_id-1.2 {no such topic} -setup {
    mymam topic add 1
    mymam topic configure 1 -name "John Doe" 
} -body {
    mymam topic id "Jane Doe"
} -cleanup {
    cleanup
} -result {}

#-------------------------------------------------------------------
# topic view

test topic_view-1.1 {retrieves the view dict} -setup {
    mymam topic add 1
} -body {
    mymam topic view 1
} -cleanup {
    cleanup
} -result {name {Topic 1} affinity 1 relevance 1.00 tid 1 aflag Yes}



#-------------------------------------------------------------------
# belief get

test belief_get-1.1 {retrieve all values} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief get 1 1
} -cleanup {
    cleanup
} -result {position 0.0 emphasis 0.5}

test belief_get-1.2 {retrieve one attribute} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief get 1 1 position
} -cleanup {
    cleanup
} -result {0.0}

test belief_get-1.3 {bad attribute name} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    catch {mymam belief get 1 1 nonesuch}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# belief set

test belief_set-1.1 {set one value} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set 1 1 position 0.5
    mymam belief get 1 1 position
} -cleanup {
    cleanup
} -result {0.5}

test belief_set-1.2 {set multiple attributes} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set 1 1 position 0.1 emphasis 0.2
    mymam belief get 1 1
} -cleanup {
    cleanup
} -result {position 0.1 emphasis 0.2}

test belief_set-1.3 {set with dict} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set 1 1 [dict create position 0.3 emphasis 0.4]
    mymam belief get 1 1
} -cleanup {
    cleanup
} -result {position 0.3 emphasis 0.4}

test belief_set-1.4 {bad system} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set nonesuch 1 position 0.1
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid system}

test belief_set-1.5 {bad topic} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set 1 nonesuch position 0.1
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid topic}

test belief_set-1.6 {bad attribute name} -setup {
    mymam topic add 1
    mymam system add 1
} -body {
    mymam belief set 1 1 nonesuch 0
} -returnCodes {
    error
} -cleanup {
    cleanup
} -result {Invalid belief attribute: nonesuch}

test belief_set-1.7 {marks changed} -setup {
    mymam system add 1
    mymam topic add 1
    mymam checkpoint -saved
} -body {
    set a [mymam changed]
    mymam belief set 1 1 position 0.5
    set b [mymam changed]

    list $a $b
} -cleanup {
    cleanup
} -result {0 1}


#-------------------------------------------------------------------
# belief cget

test belief_cget-1.1 {retrieves attribute} -setup {
    mymam topic add 1
    mymam system add 1
    mymam belief set 1 1 position 0.5
} -body {
    set a [mymam belief get  1 1 position]
    set b [mymam belief cget 1 1 -position]

    expr {$a eq $b}
} -cleanup {
    cleanup
} -result {1}

#-------------------------------------------------------------------
# belief configure

test belief_configure-1.1 {sets attributes} -setup {
    mymam topic add 1
    mymam system add 1
    mymam belief set 1 1 position 0.5
} -body {
    mymam belief configure 1 1 \
        -position 0.1 \
        -emphasis 0.3

    mymam belief get 1 1
} -cleanup {
    cleanup
} -result {position 0.1 emphasis 0.3}


#-------------------------------------------------------------------
# belief view

test belief_view-1.1 {retrieves the view dict} -setup {
    mymam system add 1
    mymam topic add 1
} -body {
    mymam belief view 1 1
} -cleanup {
    cleanup
} -result {position A emphasis NEITHER sid 1 tid 1 textpos Ambivalent textemph Neither numpos 0.0 numemph 0.5}


#-------------------------------------------------------------------
# compute

proc countAffinities {value} {
    set count 0
    foreach s1 [mymam system ids] {
        foreach s2 [mymam system ids] {
            if {[mymam affinity $s1 $s2] == $value} {
                incr count
            }
        }
    }

    return $count
}

# 1.*: Defaults, general results
test compute-1.1 {No topics; all affinities are 0.0} -setup {
    mymam system add 1
    mymam system add 2
} -body {
    mymam compute
    countAffinities 0.0
} -cleanup {
    cleanup
} -result {4}

test compute-1.2 {Default settings; all affinities are 1.0} -setup {
    mymam system add 1
    mymam system add 2
    mymam topic add 1
} -body {
    mymam compute
    countAffinities 1.0
} -cleanup {
    cleanup
} -result {4}

test compute-1.3 {No affinity topics; all affinities are 0.0} -setup {
    mymam system add 1
    mymam system add 2
    mymam topic add 1

    mymam topic configure 1 -affinity 0
} -body {
    mymam compute
    countAffinities 0.0
} -cleanup {
    cleanup
} -result {4}



# 2.*: Verify each special case in the computation

test compute-2.1 {Case A: No zeal, gamma, or zero-emphasis} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2
    
    mymam playbox configure -gamma 0.0
} -body {
    mymam compute
    format %.2f [mymam affinity 1 2]
} -cleanup {
    cleanup
} -result {0.00}

test compute-2.2 {Case B: Zero emph., but no zeal or gamma} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2
    
    mymam playbox configure -gamma 0.0
    mymam belief configure 1 1 -emphasis 0.0
} -body {
    mymam compute
    format %.2f [mymam affinity 1 2]
} -cleanup {
    cleanup
} -result {0.00}

test compute-2.3 {Case C: Pathological disagreement} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2

    # sid1 and sid2 disagree on a topic for which sid1 has emphasis 0
    mymam belief configure 1 1 -position 0.1 -emphasis 0.0
} -body {
    mymam compute
    format %.2f [mymam affinity 1 2]
} -cleanup {
    cleanup
} -result {-1.00}

test compute-2.4 {Case D: zero-emphasis with positive agreement} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2

    # sid1 and sid2 agree on all topics for which sid1 has emphasis 0,
    # and the zeals are positive
    mymam belief configure 1 1 -position 0.2 -emphasis 0.0
    mymam belief configure 2 1 -position 0.2
    mymam belief configure 1 2 -position 0.1
    mymam belief configure 2 2 -position -0.1

    # Assume no commonality.
    mymam playbox configure -gamma 0.0
} -body {
    mymam compute
    format %.2f [mymam affinity 1 2]
} -cleanup {
    cleanup
} -result {0.20}

test compute-2.5 {Case E: The normal case} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2

    # sid1 has positions on some topics, and has zero emphasis on no
    # topics.  The normal equation is used.
    mymam belief configure 1 1 -position 0.2

    # Assume no commonality.
    mymam playbox configure -gamma 0.0
} -body {
    mymam compute
    format %.2f [mymam affinity 1 2]
} -cleanup {
    cleanup
} -result {-0.09}

# 3.*: Relevance

test compute-3.1 {No relevant topics; all affinities are 0} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1 
    mymam topic configure 1 -relevance 0.0
} -body {
    mymam compute
    countAffinities 0.0
} -cleanup {
    cleanup
} -result {4}

test compute-3.2 {partial relevance} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2

    # sid1 has positions on some topics, and has zero emphasis on no
    # topics.  The normal equation is used.
    mymam belief configure 1 1 -position 1.0
} -body {
    mymam compute
    set a [mymam affinity 1 2]

    # Now, reduce the relevance of T1
    mymam topic configure 1 -relevance 0.5
    mymam compute

    set b [mymam affinity 1 2]

    # With two topics, setting the relevance of T1 to 0.5 is equivalent
    # to cutting the positions on T1 in half, and reducing the 
    # playbox commonality dial to 0.75.  This was done using MAM
    # interactively; the interactive results match the computed 
    # results shown below.
    format "%.3f %.3f" $a $b
} -cleanup {
    cleanup
} -result {0.429 0.647}

#-------------------------------------------------------------------
# affinity

# affinity is mostly tested above, under compute. 

test affinity-1.1 {caches results} -setup {
    mymam system add 1
    mymam system add 2

    mymam topic add 1
    mymam topic add 2

} -body {
    set a [mymam affinity 1 2]

    # There should only be one affinity in cache
    set result "\n[mymam dump]"
} -cleanup {
    cleanup
} -result {
playbox:     gamma 1.0
sids:        1 2
tids:        1 2

topic-1:  name {Topic 1} affinity 1 relevance 1.0
topic-2:  name {Topic 2} affinity 1 relevance 1.0

system-1: name {System 1} commonality 1.0
belief 1: 0.0 0.5]
belief 2: 0.0 0.5]

system-2: name {System 2} commonality 1.0
belief 1: 0.0 0.5]
belief 2: 0.0 0.5]

affinity 1,2 = 1.0
}




#-------------------------------------------------------------------
# congruence

proc congruenceSetup {} {
    # Two entities, so we can be sure we're getting the right one.
    mymam system add 1
    mymam system add 2

    # Three topics
    mymam topic add 1
    mymam topic add 2
    mymam topic add 3
   
    # 2's beliefs don't matter; set up 1's.
    mymam belief configure 1 1 -position 1.0
    mymam belief configure 1 2 -position 0.5
    mymam belief configure 1 3 -position -0.5
}

test congruence-1.1 {single topics} -setup {
    congruenceSetup
} -body {
    set a [mymam congruence 1 1.0 [list 1 0.5]]
    set b [mymam congruence 1 1.0 [list 2 0.5]]
    set c [mymam congruence 1 1.0 [list 3 0.5]]

    format "%.2f %.2f %.2f" $a $b $c
} -cleanup {
    cleanup
} -result {0.65 0.83 0.43}

test congruence-1.2 {single topics, theta=0} -setup {
    congruenceSetup
} -body {
    set a [mymam congruence 1 0.0 [list 1 0.5]]
    set b [mymam congruence 1 0.0 [list 2 0.5]]
    set c [mymam congruence 1 0.0 [list 3 0.5]]

    format "%.2f %.2f %.2f" $a $b $c
} -cleanup {
    cleanup
} -result {0.37 0.50 -0.33}

test congruence-1.3 {single topics, relevance=0.5} -setup {
    congruenceSetup
    mymam topic configure 1 -relevance 0.5
    mymam topic configure 2 -relevance 0.5
    mymam topic configure 3 -relevance 0.5
} -body {
    set a [mymam congruence 1 1.0 [list 1 0.5]]
    set b [mymam congruence 1 1.0 [list 2 0.5]]
    set c [mymam congruence 1 1.0 [list 3 0.5]]

    format "%.2f %.2f %.2f" $a $b $c
} -cleanup {
    cleanup
} -result {0.58 0.75 0.54}

test congruence-1.4 {multiple topics} -setup {
    congruenceSetup
} -body {
    set a [mymam congruence 1 1.0 [list 1 0.5 3 0.5]]

    format "%.2f" $a 
} -cleanup {
    cleanup
} -result {0.55}

test congruence-1.5 {multiple topics, theta=0} -setup {
    congruenceSetup
} -body {
    set a [mymam congruence 1 0.0 [list 1 0.5 3 0.5]]

    format "%.2f" $a 
} -cleanup {
    cleanup
} -result {0.10}

test congruence-1.6 {multiple topics, relevance=0.5} -setup {
    congruenceSetup
    mymam topic configure 1 -relevance 0.5
} -body {
    set a [mymam congruence 1 1.0 [list 1 0.5 3 0.5]]

    format "%.2f" $a 
} -cleanup {
    cleanup
} -result {0.49}

#-------------------------------------------------------------------
# checkpoint/restore

test checkpoint-1.1 {can checkpoint and restore} -setup {
    mymam playbox configure -gamma 2.0

    mymam system add 1
    mymam system configure 1 -name "Joe"

    mymam system add 2
    mymam system configure 1 -name "Bob"

    mymam topic add 1
    mymam topic configure 1 -name "Plain"

    mymam topic add 2
    mymam topic configure 2 -name "Peanut"

    mymam belief configure 1 1 -position 0.1
    mymam belief configure 1 2 -position 0.2
    mymam belief configure 2 1 -position 0.3
    mymam belief configure 2 2 -position 0.4
} -body {
    set a [mymam dump]

    set checkpoint [mymam checkpoint]
    mymam clear
    mymam restore $checkpoint

    set b [mymam dump]

    expr {$a eq $b}
} -result 1

#-------------------------------------------------------------------
# Cleanup

mymam destroy

tcltest::cleanupTests

