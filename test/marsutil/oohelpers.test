# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    oohelpers.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Tcltest test suite for marsutil(n) oohelpers(n).
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n)

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.2 
    eval ::tcltest::configure $argv
}

# Import tcltest(n)
namespace import ::tcltest::*

 
#-----------------------------------------------------------------------
# Load the package to be tested

source ../../lib/marsutil/pkgModules.tcl
namespace import ::marsutil::*

#-----------------------------------------------------------------------
# Test Setup

oo::class create spirit {}
oo::class create body   {}
oo::class create animal { superclass body   }
oo::class create dog    { superclass animal }
oo::class create cat    { superclass animal }


#-----------------------------------------------------------------------
# isa

test isa-1.1 {fido is a dog} -setup {
    set fido [dog new]
} -body {
    isa dog $fido
} -cleanup {
    $fido destroy
} -result {1}

test isa-1.2 {fido is not a cat} -setup {
    set fido [dog new]
} -body {
    isa cat $fido
} -cleanup {
    $fido destroy
} -result {0}

test isa-1.3 {fido is an animal} -setup {
    set fido [dog new]
} -body {
    isa animal $fido
} -cleanup {
    $fido destroy
} -result {1}

#-----------------------------------------------------------------------
# isancestor

test isancestor-1.1 {a dog is an animal} -body {
    isancestor animal dog
} -result {1}

test isancestor-1.2 {a dog is a body} -body {
    isancestor body dog 
} -result {1}

test isancestor-1.3 {a dog is not a spirit} -body {
    isancestor spirit dog 
} -result {0}

#-----------------------------------------------------------------------
# meta

test meta-1.1 {meta is added to class} -body {
    oo::class create myclass { meta title "My Class" }
    myclass title
} -cleanup {
    myclass destroy
} -result {My Class}

test meta-1.2 {meta is added to instance} -body {
    oo::class create myclass { meta title "My Class" }
    set obj [myclass new]
    $obj title
} -cleanup {
    myclass destroy
} -result {My Class}

#-----------------------------------------------------------------------
# metadict

test metadict-1.1 {metadict is added to class} -body {
    oo::class create myclass { 
        metadict lookup {A 1 B 2 C 3}
    }

    myclass lookup
} -cleanup {
    myclass destroy
} -result {A 1 B 2 C 3}

test metadict-1.2 {class can look up keys} -body {
    oo::class create myclass { 
        metadict lookup {A 1 B 2 C 3}
    }
    
    myclass lookup B
} -cleanup {
    myclass destroy
} -result {2}

test metadict-1.3 {value can span lines} -body {
    oo::class create myclass { 
        metadict lookup {
            A 1 
            B 2 
            C 3
        }
    }
    
    myclass lookup B
} -cleanup {
    myclass destroy
} -result {2}

test metadict-2.1 {metadict is added to instance} -body {
    oo::class create myclass { 
        metadict lookup {A 1 B 2 C 3}
    }
    
    set obj [myclass new]
    $obj lookup
} -cleanup {
    myclass destroy
} -result {A 1 B 2 C 3}

test metadict-2.2 {instance can look up keys} -body {
    oo::class create myclass { 
        metadict lookup {A 1 B 2 C 3}
    }
    
    set obj [myclass new]
    $obj lookup C
} -cleanup {
    myclass destroy
} -result {3}

#-----------------------------------------------------------------------
# Cleanup

cleanupTests
